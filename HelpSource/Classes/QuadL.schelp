class:: QuadL
summary:: General quadratic map chaotic generator
categories:: UGens>Generators>Chaotic
related:: Classes/QuadC, Classes/QuadN

description::
A linear-interpolating sound generator based on the difference equation:

teletype::
	x(n+1) = a * x(n)^2 + b * x(n) + c
::

sclang code translation:

code::
(
var a = 1, b = -1, c = -0.75, xi = 0, size = 64;
plot(size.collect { xi = (a * (xi ** 2)) + (b * xi) + c; xi });
)
::

classmethods::
method::ar
argument::freq
Iteration frequency in Hertz
argument::a
Equation variable
argument::b
Equation variable
argument::c
Equation variable
argument::xi
Initial value of x

examples::
code::
// default params
{ QuadL.ar(SampleRate.ir/4) * 0.2 }.play(s);
::

code::
// logistic map
// equation: x1 = -r*x0^2 + r*x0
(
{ var r;
	r = MouseX.kr(3.5441, 4);	// stable range
	QuadL.ar(SampleRate.ir/4, r.neg, r, 0, 0.1) * 0.4;
}.play(s);
)
::

code::
// logistic map as frequency control
(
{ var r;
	r = MouseX.kr(3.5441, 4);	// stable range
	SinOsc.ar(QuadL.ar(40, r.neg, r, 0, 0.1, 800, 900)) * 0.4;
}.play(s);
)
::
