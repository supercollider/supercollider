class:: Pfindur
summary:: limit total duration of events embedded in a stream
related:: Classes/Pfinval, Classes/Pfin, Classes/Pconst
categories:: Streams-Patterns-Events>Patterns>Repetition

ClassMethods::

method::new
Embeds elements of the strong::pattern:: into the stream until the duration comes close enough to strong::dur::.

argument::dur
The duration in beats after which the stream should end, derived from the event keys code::dur:: or code::delta::. The delta of the last event is adjusted so that the total duration fits this value. If the stream is shorter than dur, a code::filler:: (see below) can be used to fill the gap.

argument::pattern
The pattern to cut short. This should be an event pattern (returning a stream of events).

argument::tolerance
measuring tolerance for the total duration to avoid jitter




Examples::

code::
(
var a, b;
a = Pfindur(5, Pbind(\dur, Prand([1, 2, 0.5, 0.1], inf)));
x = a.asStream;
9.do({ x.next(Event.default).postln; });
)


(
var a, b;
a = Pfindur(5, Pbind(\dur, Prand([1, 2, 0.5, 0.1], 4)), filling: (note: 1920));
x = a.asStream;
9.do({ x.next(Event.default).postln; });
)


//Pfindur used as a sequence of pitches

(
SynthDef(\help_sinegrain,
	{ arg out=0, freq=440, sustain=0.05;
		var env;
		env = EnvGen.kr(Env.perc(0.01, sustain, 0.2), doneAction: Done.freeSelf);
		Out.ar(out, SinOsc.ar(freq, 0, env))
	}).add;
)

(
var c;
c = Pbind(
	\dur, Prand([1, 0.02, 0.2], inf),
	\instrument, \help_sinegrain,
	\degree, Pseries(0, 1, inf),
	\octave, 6
);

Pn(
	Pfindur(1, c)
).play;
)
::
