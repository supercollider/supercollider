class:: DetectIndex
summary:: Search a buffer for a value
categories:: UGens>Buffer

description::
Linearly searches a buffer for a value (i.e., loops over the frames in the
buffer from the beginning, checking for float equality), and returns index of first occurrence of said value.
Returns -1 if value is not found.
The object performs a search whenever a change in the strong::in:: argument is
detected; 
changes in the strong::bufnum:: argument do not trigger searches.
Checks for changes occur at the selected rate (ar or kr).

classmethods::
method:: ar, kr

argument:: bufnum
index of the buffer
argument:: in
the input signal.
returns::
index of first occurrence of value, or -1 if value is not found.

examples::
code::
(
var max = 300;
t = Array.series(max, 0, 1).curdle(0.06).scramble.flat;
b = Buffer(s, t.size, 1);

// alloc and set the values
s.listSendMsg(b.allocMsg(b.setnMsg(0, t)));


{
	var index, in, out, f0, fdiff;
	var bufnum = b;
	var input;
	input = MouseX.kr(0, max).round(1); // round to integer
	index = DetectIndex.kr(bufnum, input);
	index.poll;
	SinOsc.ar(index.linexp(0, max, 200, 700)) * 0.1
}.play;
)

b.free;
::
