title:: RLPF
summary:: a resonant low pass filter
categories:: UGens>Filters>Linear
related:: Classes/Formlet, Classes/RHPF, Classes/Resonz, Classes/Ringz


description::


A resonant low pass filter.


classmethods::


method::ar, kr

argument::in
the input signal.

argument::freq
cutoff frequency in Hertz.

warning::
Due to the nature of its implementation frequency values close to 0 may cause glitches and/or extremely loud audio artifacts!
::

argument::rq
the reciprocal of Q: teletype::bandwidth / cutoffFreq::.

argument::mul
output will be multiplied by this value.

argument::add
this value will be added to the output.


examples::

code::

{ RLPF.ar(Saw.ar(200, 0.1), SinOsc.ar(XLine.kr(0.7, 300, 20), 0, 3600, 4000), 0.2) }.play;


(
{ var ctl = RLPF.ar(Saw.ar(5, 0.1), 25, 0.03);
	SinOsc.ar(ctl * 200 + 400) * 0.1;
}.play;
)


(
{ var ctl = RLPF.ar(Saw.ar(5, 0.1), MouseX.kr(2, 200, 1), MouseY.kr(0.01, 1, 1));
	SinOsc.ar(ctl * 200 + 400) * 0.1;
}.play;
)

::
