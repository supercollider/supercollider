class:: ServerOptions
summary:: Encapsulates commandline and other options for a Server
categories:: Server
related:: Classes/Server, Reference/Server-Architecture, Reference/Server-Command-Reference

description::

ServerOptions encapsulates various options for a server app within an object. This makes it convenient to launch multiple servers with the same options, or to archive different sets of options, etc.
Every link::Classes/Server:: has an instance of ServerOptions created for it if one is not passed as the options argument when the Server object is created. (This is the case for example with the local and internal Servers which are created at startup.)

A Server's instance of ServerOptions is stored in its options instance variable, which can be accessed through corresponding getter and setter methods.

N.B. A ServerOptions' instance variables are translated into commandline arguments when a server app is booted. Thus a running Server must be rebooted before changes will take effect. There are also a few commandline options which are not currently encapsulated in ServerOptions. See link::Reference/Server-Architecture:: for more details.

ClassMethods::
private:: prListDevices

method:: new
Create and return a new instance of ServerOptions.

method:: devices
Return an Array of Strings listing the audio devices currently available on the system.

method:: inDevices
Return an Array of Strings listing the audio devices currently available on the system which have input channels.

method:: outDevices
Return an Array of Strings listing the audio devices currently available on the system which have output channels.

instancemethods::
subsection:: The Options

method:: blockSize
The number of samples in one control period. The default is 64.

method:: device
A String that allows you to choose a sound device to use as input and output. The default, nil will use the system's default input and output device(s) (more below in the examples).

note::When the server is compiled for jack natively, the teletype::device:: can be used to connect to a named server and
use a specific client name. The argument specifies either a server name or a server name and the requested client
name. Passing code::nil:: is equivalent to to teletype::default:SuperCollider::.

The jack connections can be configured via the environment variables teletype::SC_JACK_DEFAULT_INPUTS:: and
teletype::SC_JACK_DEFAULT_OUTPUTS::. The format is either a string that specifies the another jack client or a
comma-separate list of jack ports.

code::
// connect first to input channels with system
"SC_JACK_DEFAULT_INPUTS".setenv("system:capture_1,system:capture_2");

// connect all output channels with system
"SC_JACK_DEFAULT_OUTPUTS".setenv("system");
::

::

method:: inDevice
A String that allows you to choose an input sound device. The default, nil will use the system's default input device (more below in the examples).

method:: outDevice
A String that allows you to choose an output sound device. The default, nil will use the system's default output device (more below in the examples).

method:: hardwareBufferSize
The preferred hardware buffer size. If non-nil the server app will attempt to set the hardware buffer frame size. Not all sizes are valid. See the documentation of your audio hardware for details. Default value is nil.

method:: initialNodeID
By default, the Server object in the client begins allocating node IDs at 1000, reserving 0-999 for "permanent" nodes. You may change this default here.

method:: inputStreamsEnabled
A String which allows turning off input streams that you are not interested in on the audio device. If the string is "01100", for example, then only the second and third input streams on the device will be enabled. Turning off streams can reduce CPU load. The default value is nil.

method:: loadDefs
A Boolean indicating whether or not to load the synth definitions in synthdefs/ (or anywhere set in the environment variable SC_SYNTHDEF_PATH) at startup. The default is true.

method:: maxNodes
The maximum number of nodes. The default is 1024.

method:: maxSynthDefs
The maximum number of synthdefs. The default is 1024.

method:: memSize
The number of kilobytes of real time memory allocated to the server. This memory is used to allocate synths and any memory that unit generators themselves allocate (for instance in the case of delay ugens which do not use buffers, such as CombN), and is separate from the memory used for buffers. Setting this too low is a common cause of 'exception in real time: alloc failed' errors. The default is 8192.

method:: numAudioBusChannels
The number of audio rate busses, which includes input and output busses. The default is 128.

method:: numBuffers
The number of global sample buffers available. (See Buffer.) The default is 1024.

method:: numControlBusChannels
The number of internal control rate busses. The default is 4096.

method:: numInputBusChannels
The number of audio input bus channels. This need not correspond to the number of hardware inputs. The default is 2.

method:: numOutputBusChannels
The number of audio output bus channels. This need not correspond to the number of hardware outputs (this can be useful for instance in the case of recording). The default is 2.

method:: numRGens
The number of seedable random number generators. The default is 64.

method:: numWireBufs
The maximum number of buffers that are allocated to interconnect unit generators. (Not to be confused with the global sample buffers represented by Buffer.) This sets the limit of complexity of SynthDefs that can be loaded at runtime. This value will be automatically increased if a more complex def is loaded at startup, but it cannot be increased thereafter without rebooting. The default is 64.

method:: outputStreamsEnabled
A String which allows turning off output streams that you are not interested in on the  audio device. If the string is "11000", for example, then only the first two output streams on the device will be enabled. Turning off streams can reduce CPU load.

method:: protocol
A Symbol representing the communications protocol. Either code::\udp:: or code::\tcp::. The default is code::\udp::.

method:: remoteControlVolume
A Boolean indicating whether this server should allow its volume to be set remotely. The default value is code::false::.

method:: sampleRate
The preferred sample rate. If non-nil the server app will attempt to set the sample rate of the hardware. The hardware has to support the sample rate that you choose.

method:: verbosity
Controls the verbosity of server messages. A value of 0 is normal behaviour, -1 suppresses informational messages, and -2 suppresses informational and many error messages. The default is 0.

method:: zeroConf
A Boolean indication whether or not the server should publish its port using zero configuration networking, to facilitate network interaction. This is true by default; if you find unacceptable delays (beachballing) upon server boot, you can try setting this to false.

method:: ugenPluginsPath
A path or an Array of paths. If non-nil, the standard paths are NOT searched for plugins. This corresponds with the option "-U".

method:: restrictedPath
Allows you to restrict the system paths in which scsynth is allowed to read/write files during running. A nil value (the default) means no restriction. Otherwise, set it as a string representing a single path.

method:: threads
Number of audio threads that are spawned by supernova. For scsynth this value is ignored. If it is code::nil::or 0, it
uses the one thread per CPU. Default is code::nil::.

method:: useSystemClock
Tells supernova whether to sync to the driver's sample clock, or to the system clock.
For scsynth this value is ignored.
list::
## code::false:: (default) -- Use the sample clock. This helps to
support sample-accurate scheduling; however, messaging latency from
the SuperCollider language will drift over long periods of time.
## code::true:: -- Use the system clock. Timestamped messages will maintain consistent
latency over long sessions, but may not be perfectly sample-accurate.
::

method:: memoryLocking
A Boolean indicating whether the server should try to lock its memory into physical RAM. Default is code::false::.

method:: maxLogins
An Integer indicating the maximum number of clients which can simultaneously receive notifications from the server. When using TCP this is also the maximum number of simultaneous connections. This is also used by the language to split ranges of link::Classes/Node##Nodes::, link::Classes/Buffer##Buffers::, or link::Classes/Bus##Busses::. In multi-client situations you will need to set this to at least the number of clients you wish to allow. This must be the same in the Server instances on every client. The default is 1.


subsection:: Other Instance Methods
method:: asOptionsString
argument:: port
The port number for the resulting server app. Default value is 57110.
Returns:: a String specifying the options in the format required by the command-line scsynth app.

method:: firstPrivateBus
Returns:: the index of the first audio bus on this server which is not used by the input and output hardware.

method:: pingsBeforeConsideredDead
number of failed pings (attempts to contact server process) before server is considered dead.

Examples::
code::
// Get the local server's options

o = Server.local.options;

// Post the number of output channels

o.numOutputBusChannels.postln;

// Set them to a new number

o.numOutputBusChannels = 6; // The next time it boots, this will take effect

// specify a device

o.device ="MOTU Traveler"; 	// use a specific soundcard
o.device = nil;			// use the system default soundcard

// Create a new instance of ServerOptions

o = ServerOptions.new;

// Set the memory size to twice the default

o.memSize = 4096;

// Create a new Server on the local machine using o for its options

t = Server(\Local2, NetAddr("127.0.0.1", 57111), o);
t.makeWindow;
t.boot;
t.quit;
::

