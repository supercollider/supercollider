class:: Dseries
summary:: Demand rate arithmetic series UGen.
related:: Classes/Demand, Classes/Dgeom, Classes/Duty, Classes/TDuty
categories:: UGens>Demand

Description::

Demand rate arithmetic series UGen.

See link::Classes/Pseries:: for structurally related equivalent.


classmethods::

method::new

argument::start

Start value. Can be a number or any other UGen.


argument::step

Step value. Can be a number or any other UGen.


argument::length

Number of values to create.  Can be a number or any other UGen.


Examples::

code::

(
{
	var a, freq, trig;
	a = Dseries(0, 1, 15);
	trig = Impulse.kr(MouseX.kr(1, 40, 1));
	freq = Demand.kr(trig, 0, a) * 30 + 340;
	SinOsc.ar(freq) * 0.1

}.play;
)

(
{
	var a, freq, trig;
	a = Dseries(0, 1, inf);
	trig = Dust.kr(MouseX.kr(1, 40, 1));
	freq = Demand.kr(trig, 0, a) % 15 * 30 + 340;
	SinOsc.ar(freq) * 0.1

}.play;
)

::

