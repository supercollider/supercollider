title:: Pn
categories:: Streams-Patterns-Events>Patterns>Repetition
summary:: Repeatedly embed a pattern
related:: Classes/Pdup










classmethods::




method:: new


argument:: pattern

The pattern to repeat


argument:: repeats

Repeat the enclosed pattern strong::repeats:: times.


argument:: key

If strong::key:: is non-nil, it sets the value of that key to teletype::true:: whenever it restarts the pattern. This can be used to advance Patterns enclosed by link::Classes/Pgate::.










examples::




code::
(
var a, b;
a = Pn(Pseq(#[1, 2, 3], 1), 4);	// repeat pattern four times
b = a.asStream;
16.do({ b.next.postln });
)

// sound example
(
SynthDef(\help_sinegrain,
	{ |out = 0, freq = 440, sustain = 0.05|
		var env;
		env = EnvGen.kr(Env.perc(0.01, sustain, 0.2), doneAction: Done.freeSelf);
		Out.ar(out, SinOsc.ar(freq, 0, env))
	}).add;
)

(
var a;
a = Pn(Pshuf([1, 2, 2, 3, 3, 3], 4)).asStream;
{
	loop {
		Synth(\help_sinegrain, [\freq, a.next * 600 + 300]);
		0.2.wait;
	}
}.fork;
)
::
