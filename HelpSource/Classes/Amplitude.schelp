class:: Amplitude
summary:: Amplitude follower
categories:: UGens>Analysis>Amplitude

description::
Tracks the peak amplitude of a signal.

classmethods::

method:: ar, kr

argument::in
Input signal

argument::attackTime
60dB convergence time for following attacks.

argument::releaseTime
60dB convergence time for following decays.

argument::mul

argument::add

examples::

code::
(
// use input amplitude to control SinOsc frequency
{
    SinOsc.ar(
            Amplitude.kr(
                        Blip.ar(3, 20) * LFNoise1.kr(0.1).range(0, 1),
                        MouseX.kr(0.001, 1, 1),
                        MouseY.kr(0.001, 1, 1),
                        1200,
                        400
            ),
            0, 0.3)
}.play;
)
::

code::
// use input amplitude to control Pulse amplitude - use headphones to prevent feedback.
{ Pulse.ar(90, 0.3, Amplitude.kr(SoundIn.ar(0))) }.play;
::

code::
(
// use input amplitude to control SinOsc frequency - use headphones to prevent feedback.
{
    SinOsc.ar(
            Amplitude.kr(
                        SoundIn.ar(0),
                        0.01,
                        0.01,
                        1200,
                        400
            ),
            0, 0.3)
}.play;
)
::
