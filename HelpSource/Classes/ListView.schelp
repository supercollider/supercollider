CLASS:: ListView
redirect:: implClass
summary:: A view displaying a list of text items.
categories:: GUI>Views

DESCRIPTION::

A view that displays a list of text items and allows one of them to be selected.

An item can be selected by clicking on it. Pressing the up or down arrow keys while an item is selected will select the previous or next item respectively (if available).

CLASSMETHODS::

PRIVATE:: key




INSTANCEMETHODS::



SUBSECTION:: Data

METHOD:: items
	The list of items displayed by the view.

	argument::
		An Array of Strings, each String defining the text to represent an item.

METHOD:: clear
    note:: Only available in Qt GUI ::
    Removes all items.

METHOD:: value
	The index of the currently selected item, or nil when no item is selected.

	argument::
		An Integer or nil.

METHOD:: valueAction
	Sets link::#-value:: and triggers the link::#-action::.

METHOD:: selection
	note::Only in Qt GUI::
	Returns an array of all selected indexes. See also: link::#-selectionMode::.


SUBSECTION:: Appearance

METHOD:: colors
	The background colors of the items.

	argument::
		An Array of Colors, one Color for each item.

METHOD:: stringColor
	The color used to display all the text of all unselected items.

	argument::
		A Color.

METHOD:: selectedStringColor
	The color used to display the selected item's text.

	argument::
		A Color.

METHOD:: hiliteColor
	The color used to indicate the selected item (aside from the color of its text).

	argument::
		A Color.




SUBSECTION:: Interaction

METHOD:: selectionMode
	note:: Only available in Qt GUI ::

	The allowed mode of item selection, according to the following table:

	table::
	## strong::Value::  || strong::Meaning::
	## \none            || No item can be selected.
	## \single          || Only a single item can be selected at once.
	## \multi           || Multiple items can be selected. An item's selection is toggled when clicked.
	## \extended        || Multiple items can be selected, individually by holding the Ctrl key, and in a batch by holding the Shift key.
	## \contiguous      || Mutliple neighbouring items can be selected by holding the Shift key.
	::

	argument::
		One of the Symbols listed in the table above.



SUBSECTION:: Actions

METHOD:: action
	The action object evaluated whenever the user changes the item selection.

METHOD:: enterKeyAction
	The action object evaluated whenever the user presses the Enter (Return) key.

METHOD:: defaultKeyDownAction

	Implements the default effects of key presses as follows:

	table::
	## strong::Key::   || strong::Effect::
	## space           || select next item and trigger action
	## r               || trigger enterKeyAction
	## n               || trigger enterKeyAction
	## a number        || trigger enterKeyAction
	## up arrow        || select previous item and trigger action
	## down arrow      || select next item and trigger action
	## left arrow      || select previous item and trigger action
	## right arrow     || select next item and trigger action
	::



SUBSECTION:: Drag and drop

METHOD:: defaultGetDrag
	returns::
		The link::#-value::.

METHOD:: defaultCanReceiveDrag
	returns::
		True if the current drag data is a number.

METHOD:: defaultReceiveDrag
	Sets link::#-valueAction:: to the current drag data.


EXAMPLES::

subsection:: Basic Example

code::
(
w = Window.new.front;

v = ListView(w,Rect(10,10,120,70))
    .items_([ "SinOsc", "Saw", "LFSaw", "WhiteNoise", "PinkNoise", "BrownNoise", "Osc" ])
    .background_(Color.clear)
    .hiliteColor_(Color.green(alpha:0.6))
    .action_({ arg sbs;
        [sbs.value, v.items[sbs.value]].postln; // .value returns the integer
    });
)
::


subsection:: Sound Example

Use ListView to switch filters:
code::
(
Server.default = s = Stethoscope.defaultServer.boot;
s.waitForBoot({
    var f, freq, ww;
    n={r=LFSaw.ar([220, 530],0,0.3)*LFPulse.ar(12,0,0.3,0.4); [r[0],Delay2.ar(r[1])]}.play;
    freq={SinOsc.kr(0.5,0,4000,4200)}.play;
    w=Window("Filters").front;
    v = ListView(w,Rect(10,10,180,120))
        .items_([ "No Filter","RLPF", "RHPF", "BPF", "Resonz", "MoogFF" ])
        .background_(Color.clear)
        .hiliteColor_(Color.green(alpha:0.6))
        .action_({arg v;
            v.value.switch(
            0,{try{f.free};"test".postln},
            1,{try{f.free};f={ReplaceOut.ar(0,RLPF.ar(In.ar(0,2),In.kr(0,1),0.2,0.3))}.play(addAction:\addToTail)},
            2,{try{f.free};f={ReplaceOut.ar(0,RHPF.ar(In.ar(0,2),In.kr(0,1),0.2,0.3))}.play(addAction:\addToTail)},
            3,{try{f.free};f={ReplaceOut.ar(0,BPF.ar(In.ar(0,2),In.kr(0,1),0.2,1.5))}.play(addAction:\addToTail)},
            4,{try{f.free};f={ReplaceOut.ar(0,Resonz.ar(In.ar(0,2),In.kr(0,1),0.2,2))}.play(addAction:\addToTail)},
            5,{try{f.free};f={ReplaceOut.ar(0,MoogFF.ar(In.ar(0,2),In.kr(0,1),1.5))}.play(addAction:\addToTail)}
            );
        });

    ww=FreqScope.new(400, 200, 0);
    w.bounds=Rect(50,Window.screenBounds.height-300,200,200);
    ww.window.bounds=ww.window.bounds.moveTo(255,Window.screenBounds.height-328);
    CmdPeriod.doOnce({{ww.window.close}.defer(0.5);w.close;});
            //defer or crash, because FreqScopeWindow Class contains its own routine for cleaning up on CmdPeriod
    w.onClose_({n.free;f.free;freq.free});
});
)
::
