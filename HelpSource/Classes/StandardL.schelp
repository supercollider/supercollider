class:: StandardL
summary:: Standard map chaotic generator
categories:: UGens>Generators>Chaotic
related:: Classes/StandardN

description::
A linear-interpolating sound generator based on the difference equations:

teletype::
	x(n+1) = (x(n) + y(n+1)) % 2pi
	y(n+1) = (y(n) + k * sin(x(n))) % 2pi
::

The standard map is an area preserving map of a cylinder discovered by the plasma physicist Boris Chirikov.

sclang code translation:

code::
(
var k = 1, xi = 0.5, yi = 0, size = 64;
plot(size.collect { yi = yi + (k * sin(xi)) % 2pi; xi = (xi + yi) % 2pi; xi - pi * 0.3183098861837907 });
)
::

classmethods::
method:: ar
argument:: freq
Iteration frequency in Hertz
argument:: k
Perturbation amount
argument:: xi
Initial value of x
argument:: yi
Initial value of y

examples::
code::
// vary frequency
{ StandardL.ar(MouseX.kr(20, SampleRate.ir)) * 0.3 }.play(s);
::

code::
// mouse-controlled param
{ StandardL.ar(SampleRate.ir/2, MouseX.kr(0.9,4)) * 0.3 }.play(s);
::

code::
// as a frequency control
{ SinOsc.ar(StandardL.ar(40, MouseX.kr(0.9,4))*800+900)*0.4 }.play(s);
::
