CLASS:: Rational
summary:: Rational number
categories:: Math
related:: Classes/SimpleNumber, Classes/Float, Classes/Integer

DESCRIPTION::
A rational number. Alternative syntax: code::x %/ y::

note::
Operations with link::Classes/Rational:: can be one order of magnitude slower than link::Classes/Float::. Rational internally represents numerator and denominator as 64-bit link::Classes/Float::s
::

CLASSMETHODS::

method:: new
Rational also accepts Floats and Strings in its code::new:: instantiation method. This class will always reduce a rational expression to its lowest terms (e.g. code::3 %/ 2:: instead of code::6 %/ 4::).

code::
Rational(6,4) // -> 3 %/ 2
Rational("8/4") // -> 2 %/ 1
Rational(0.33333) // -> 1 %/ 3
::

INSTANCEMETHODS::

method:: +
Addition
code::
Rational(3,2) + Rational(3,2) // -> 3 %/ 1
(3 %/ 2) + (3 %/ 2) // -> 3 %/ 1
::

method:: -
Subtraction
code::
Rational(6,5) - Rational(1,3) // -> 13 %/ 15
(6 %/ 5) - (1 %/ 3) // -> 13 %/ 15
::

method:: *
Multiplication
code::
Rational(3,7) * Rational(9,5) // -> 27 %/ 35
(3 %/ 7) * (9 %/ 5) // -> 27 %/ 35
::

method:: /
Division
code::
Rational(5,3) / Rational(7,5) // -> 25 %/ 21
(5 %/ 3) + (7 %/ 5) // -> 25 %/ 21
::

method:: abs
Absolute value.
code::
Rational(-5,4).abs // -> 5 %/ 4
::

method:: asFloat
As link::Classes/Float::
code::
Rational(5,7).asFloat // -> 0.71428571428571
::

method:: pow
code::
Rational(1,2).pow(2)
// same as:
(1 %/ 2) ** 2 // -> 1 %/ 4

Rational(4,5).pow(-4) // -> 625 %/ 256
::

method:: reciprocal
Reciprocal
code::
Rational(5,7).reciprocal // -> 7 %/ 5
::

method:: squared
Power of two
code::
Rational(5,7).squared // -> 25 %/ 49
::

EXAMPLES::

Basic operations:

code::
a = Rational(3, 2); // a rational number
b = 4%/5;  // // another rational number
c = 1 %/ (1 + (1 %/ (1 + (1 %/ 2)))); // -> 3 %/ 5

a + b; // -> 23 %/ 10
a - b; // -> 7 %/ 10
a / b; // -> 15 %/ 8
a * b; // -> 6 %/ 5
a.pow(2); // -> 9 %/ 4

a.min(b); // -> 4 %/ 5
b.max(2.32); // -> 2.32

// force denominator to be less than 20:
(4342%/3424).simplify(20); // -> 19 %/ 15
::

SUBSECTION:: Sorting

code::
// sort Array of Rationals:
d = [ 0 %/ 1, 3 %/ 5, -3 %/ 4, -1 %/ 5, 3 %/ 4, -2 %/ 3, 7 %/ 8, 1 %/ 1, -3 %/ 1, 1 %/ 6, 1 %/ 6, 7 %/ 8, -10 %/ 7, 0 %/ 1, 0 %/ 1, 5 %/ 3, 10 %/ 1, 3 %/ 5, 0 %/ 1, -2 %/ 7 ];

d.sort;

// sort Array with Floats and Rationals:
e = [1, 1.5, 3 %/ 5, 8 %/ 5, 1.342];
e.sort;
::

SUBSECTION:: Continued fractions

Recursive function to calculate finite continued fractions:

code::
f = { |x| if(x == 0) { 1 } {  1 + Rational(1, f.(x-1))} };
a = 20.collect({|i| f.(i)});
a.asFloat;
a.plot;
::


SUBSECTION:: Conversion between Rational and Float

Conversion between link::Classes/Float:: and Rational can have different degrees of precision:

code::
pi; // 3.1415926535898
a = pi.asRational(99999); // 208341 %/ 66317
b = pi.asRational(999);   // 355 %/ 113
c = pi.asRational(99);    // 22 %/ 7
a.asFloat; // 3.1415926534674
b.asFloat; // 3.141592920354
c.asFloat; // 3.1428571428571
pi.equalWithPrecision(a.asFloat, 0.000000001); // true
pi.equalWithPrecision(b.asFloat, 0.00001);     // true
pi.equalWithPrecision(c.asFloat, 0.01);        // true
::
