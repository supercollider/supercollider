class:: Pswitch
summary:: embed values in a list according to a pattern of indices
related:: Classes/Pswitch1
categories:: Streams-Patterns-Events>Patterns>List>Indexing

ClassMethods::

method::new
Pswitch chooses elements from the strong::list:: by a stream of indices ( strong::which:: ) and embeds them in the stream. If the element is itself a pattern, it first completely embeds it before looking for the next index.

Examples::

code::
(
var a, b;
a = Pseq([1, 2, 3], 2);
b = Pseq([65, 76]);
c = Pswitch([a, b, 800], Pseq([2, 2, 0, 1], inf));
x = c.asStream;
24.do({ x.next.postln; });
)


//Pswitch used as a sequence of pitches:

(
SynthDef(\help_sinegrain,
	{ arg out=0, freq=440, sustain=0.05;
		var env;
		env = EnvGen.kr(Env.perc(0.01, sustain, 0.2), doneAction: Done.freeSelf);
		Out.ar(out, SinOsc.ar(freq, 0, env))
	}).add;
)

(
a = Pseq([73, 71, 69], 2);
b = Pseq([0, 0, 0, 4, 0]+64);
c = Pswitch([a, b, 75], Pseq([2, 2, 0, 1], inf));
x = c.asStream;
Routine({
	loop({
		Synth(\help_sinegrain, [\freq, x.next.midicps]);
		0.18.wait;
	})
}).play;
)
::
