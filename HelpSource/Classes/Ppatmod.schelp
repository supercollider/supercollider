title:: Ppatmod
summary:: modify a given pattern before passing it into the stream
categories:: Streams-Patterns-Events>Patterns>Filter
related:: Classes/Plazy


classmethods::


method::new

argument::pattern
the pattern.

argument::func
a link::Classes/Function:: that modifies the enclosed pattern and embeds it in the stream.

argument::repeats
the number of repeats.


examples::

code::
(
a = Ppatmod(
	Pseq([0, 0, 0, 0], 1),
	{ |pat, i|
		var list;
		list = pat.list;
		pat.list = list.put(list.size.rand, 2);
	}, inf);

x = a.asStream;
30.do({ x.next.postln });
)


// Ppatmod used to modify a pattern that produces a sequence of pitches:

(
SynthDef(\help_sinegrain,
	{ |out = 0, freq = 440, sustain = 0.05|
		var env;
		env = EnvGen.kr(Env.perc(0.01, sustain, 0.2), doneAction: Done.freeSelf);
		Out.ar(out, SinOsc.ar(freq, 0, env))
	}).add;
)

(
a = Pn(
	Ppatmod(
		Pseq([0, 0, 0, 0], 1),
		{ |pat, i|
			var list;
			list = pat.list;
			pat.list = list.put(list.size.rand, 2);
		}, 15),
inf).asStream;

Routine({
	loop({
		Synth(\help_sinegrain, [\freq, (a.next*5+77).midicps]);
		0.13.wait;
	})
}).play;
)
::
