class:: GrayNoise
summary:: Gray Noise.
related:: Classes/BrownNoise, Classes/ClipNoise, Classes/PinkNoise, Classes/WhiteNoise
categories::  UGens>Generators>Stochastic



Description::

Generates noise which results from flipping random bits in a word.footnote::Generated through math::X_{0} = 1;\;X_{n+1} = X_{n} \oplus z::
, where math::\oplus:: is bitwise exclusive or, and math::z:: is a 32-bit binary number with exactly one nonzero digit; the position of the nonzero digit is a uniform random variable.
::

This type of noise has a high RMS level relative to its peak to peak level.
The spectrum is emphasized towards lower frequencies.

note::link::Classes/GrayNoise:: is derived from digital signal characteristics (e.g., bit-flipping errors) and should not be confused with "gray noise" or "grey noise," which typically refers to noise adjusted to human auditory sensitivity curves, such as the A-weighting curve. See link::https://en.wikipedia.org/wiki/Grey_noise::::



classmethods::

method::ar, kr

argument::mul
Output will be multiplied by this value.

argument::add
This value will be added to the output.

Examples::

code::
(
SynthDef("help-GrayNoise", { |out = 0|
	Out.ar(out,
		GrayNoise.ar(0.1)
	)
}).play;
)
::

Comparison with usual gray noise:
code::
{ GrayNoise.ar * 0.1 }.freqscope

(
{
    var binsz = s.sampleRate / 2048;
    var in, chain;
    in = WhiteNoise.ar(0.1);
    chain = FFT(LocalBuf(2048), in);
    chain = chain.pvcollect(1024, { |m, p, b, i|
            m * AmpCompA.ir(b * binsz, minAmp: 0.2)
            });
    IFFT(chain);
}.freqscope
)
::
