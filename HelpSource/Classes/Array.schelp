class:: Array
summary:: fixed size collection
related:: Reference/Literals, Classes/List
categories:: Collections>Ordered

description::
Arrays are ArrayedCollections whose slots may contain any object. Arrays have a fixed maximum size beyond which they cannot grow. For expandable arrays, use the link::Classes/List:: class.

strong::Literal Arrays:: can be created at compile time, and are very efficient. See link::Reference/Literals:: for information.

For handling strong::multidimensional arrays::, there are specific methods which are covered in the helpfile link::Guides/J-concepts-in-SC::.

note::
For Arrays, the code::add:: method may or may not return the same Array object. It will add the argument to the receiver if there is space, otherwise it returns a new Array object with the argument added. Thus the proper usage of code::add:: with an Array is to always assign the result as follows:
code::
    z = z.add(obj);
::
This allows an efficient use of resources, only growing the array when it needs to. The link::Classes/List:: class manages the Array internally, and in many cases is more suitable.
::

Elements can be put into an existing slot with code::a.put(2,obj):: and accessed with
code::a.at(2):: or code::a[2]::

See link::Classes/ArrayedCollection:: for the principal methods: at, put, clipAt, wrapAt, etc...


ClassMethods::

method::new
Create a new array with size 0 that can grow up to the fixed size.
argument::maxSize
The maximum size of the array.

method::newClear
Create a new array with all slots filled with nils.
argument::indexedSize
The size of the array.

method::with
Create a new Array whose slots are filled with the given arguments.
This is the same as the method in ArrayedCollection, but is reimplemented here to be more efficient.
code::
Array.with(7, 'eight',  9).postln;
::

copymethod:: Collection *fill

copymethod:: Collection *fill2D

copymethod:: Collection *fillND

copymethod:: Collection *newFrom

copymethod:: ArrayedCollection *geom

copymethod:: ArrayedCollection *series

copymethod:: ArrayedCollection *iota

copymethod:: SequenceableCollection *interpolation

copymethod:: SequenceableCollection *rand

copymethod:: SequenceableCollection *rand2

copymethod:: SequenceableCollection *linrand

copymethod:: SequenceableCollection *exprand

copymethod:: SequenceableCollection *fib





InstanceMethods::

copymethod:: ArrayedCollection -at

copymethod:: ArrayedCollection -put

copymethod:: ArrayedCollection -insert

copymethod:: ArrayedCollection -clipAt

copymethod:: ArrayedCollection -wrapAt

copymethod:: ArrayedCollection -foldAt

copymethod:: ArrayedCollection -clipPut

copymethod:: ArrayedCollection -wrapPut

copymethod:: ArrayedCollection -foldPut

copymethod:: ArrayedCollection -swap

copymethod:: ArrayedCollection -replace

copymethod:: ArrayedCollection -++

copymethod:: ArrayedCollection -add

copymethod:: ArrayedCollection -addAll

copymethod:: ArrayedCollection -addFirst

copymethod:: ArrayedCollection -removeAt

copymethod:: ArrayedCollection -collect

copymethod:: ArrayedCollection -do

copymethod:: ArrayedCollection -reverseDo

copymethod:: ArrayedCollection -deepCollect

copymethod:: ArrayedCollection -reshape

copymethod:: ArrayedCollection -windex

copymethod:: ArrayedCollection -size

copymethod:: ArrayedCollection -normalize

copymethod:: ArrayedCollection -normalizeSum

copymethod:: ArrayedCollection -plot

method::reverse
Returns a new Array whose elements are reversed. The receiver is unchanged.
code::
x = [1, 2, 3];
z = x.reverse;
x.postln;
z.postln;
::

method::scramble
Returns a new Array whose elements have been scrambled. The receiver is unchanged.
code::
[1, 2, 3, 4, 5, 6].scramble.postln;
::

method::mirror
Return a new Array which is the receiver made into a palindrome.
The receiver is unchanged.
code::
[1, 2, 3, 4].mirror.postln;
::

method::mirror1
Return a new Array which is the receiver made into a palindrome with the last element removed.
This is useful if the list will be repeated cyclically, the first element will not get played twice.
The receiver is unchanged.
If the receiver is a single-element array, a copy is returned.
code::
[1, 2, 3, 4].mirror1.postln;
::

method::mirror2
Return a new Array which is the receiver concatenated with a reversal of itself.
The center element is duplicated. The receiver is unchanged.
code::
[1, 2, 3, 4].mirror2.postln;
::

method::stutter
note::It is recommended to use code::dupEach:: instead. This method is retained for backwards compatibility.::
Return a new Array whose elements are repeated n times. The receiver is unchanged.
code::
[1, 2, 3].stutter(2).postln;
::
argument::n
Number of repeats.

method::dupEach
Return a new Array whose elements are repeated n times. The receiver is unchanged.
code::
[1, 2, 3].dupEach(2).postln;
::
argument::n
Number of repeats.

method::rotate
Return a new Array whose elements are in rotated order. The receiver is unchanged.
code::
[1, 2, 3, 4, 5].rotate(1).postln;
[1, 2, 3, 4, 5].rotate(-1).postln;
[1, 2, 3, 4, 5].rotate(3).postln;
::
argument::n
Number of elements to rotate. Negative n values rotate left, positive n values
rotate right.

method::pyramid
Return a new Array whose elements have been reordered via one of 10 "counting" algorithms.
Run the examples to see the algorithms.
code::
10.do({ arg i;
	[1, 2, 3, 4].pyramid(i + 1).postcs;
});
::
argument::patternType
Choose counting algorithm. The algorithms are numbered 1 through 10.

method::pyramidg
Like pyramid, but keep the resulting values grouped in subarrays.
code::
// compare:
[1, 2, 3, 4].pyramid(1).postln;
[1, 2, 3, 4].pyramidg(1).postln;
::

method::sputter
Return a new Array of length maxlen with the items partly repeated (random choice of given probability).
code::
// compare:
[1, 2, 3, 4, 5, 6, 7, 8, 9, 10].sputter(0.5, 16).postln;
[1, 2, 3, 4, 5, 6, 7, 8, 9, 10].sputter(0.8, 8).postln;
::
argument::probability
Probability of repeat.
argument::maxlen
The length of the new Array.

method::lace
Returns a new Array whose elements are interlaced sequences of the elements of the receiver's subcollections, up to size length. The receiver is unchanged.
code::
x = [ [1, 2, 3], 6, List["foo", 'bar']];
y = x.lace(12);
x.postln;
y.postln;
::

method::permute
Returns a new Array whose elements are the nthPermutation of the elements of the receiver. The receiver is unchanged.
code::
x = [ 1, 2, 3];
6.do({|i| x.permute(i).postln;});
::

method::allTuples
Returns a new Array whose elements contain all possible combinations of the receiver's subcollections.
code::
[[1, 2, 3, 4, 5], [10, 20, 30]].allTuples;
[[1, 2, 3, 4, 5], [10, 20, 30], [5, 6]].allTuples;
::

method::wrapExtend
Returns a new Array whose elements are repeated sequences of the receiver, up to size length. The receiver is unchanged.
code::
x = [ 1, 2, 3, "foo", 'bar' ];
y = x.wrapExtend(9);
x.postln;
y.postln;
::

method::foldExtend
Same as wrapExtend but the sequences fold back on the list elements.
code::
x = [ 1, 2, "foo"];
y = x.foldExtend(9);
x.postln;
y.postln;
::

method::clipExtend
Same as wrapExtend but the sequences "clip" (return their last element) rather than wrapping.
code::
x = [ 1, 2, "foo"];
y = x.clipExtend(9);
x.postln;
y.postln;
::

method::slide
Return a new Array whose elements are repeated subsequences from the receiver.
Easier to demonstrate than explain.
code::
[1, 2, 3, 4, 5, 6].slide(3, 1).postcs;
[1, 2, 3, 4, 5, 6].slide(3, 2).postcs;
[1, 2, 3, 4, 5, 6].slide(4, 1).postcs;
::

method::shift
Shift the values of the array n steps to the right (n positive) or to the left(n negative),
dropping the excess and filling empty space with zero.
code::
[1, 2, 3, 4, 5, 6].shift(3).postln;
[1, 2, 3, 4, 5, 6].shift(-3).postln;
::

method::containsSeqColl
Returns true if the receiver Array contains any instance of SequenceableCollection
code::
[1, 2, 3, 4].containsSeqColl.postln
[1, 2, [3], 4].containsSeqColl.postln
::

method::powerset
Returns all possible combinations of the array's elements.
code::
[1, 2, 3].powerset.postln
[1, 2, 3].powerset.sort({ |a, b| a.size > b.size }); // sort by size, big first
[1, 2, 3].powerset.sort({ |a, b| a.size > b.size }).reverse; // by size, small first
::
powerset is also supported in Collection:
code::
Set[1, 2, 3].powerset;
List[1, 2, 3].powerset
(a: 1, b: 2, c: 3).powerset;
::

method::envirPairs
Given an array of symbols, this returns an array of pairs of (symbol, value) from the current environment.
This can then be used as arguments for a Synth, or in an OSC message.
code::
e = (freq: 340, amp: 0.001, strangeness: 0.85);
e.use {
	[\amp, \taste, \strangeness].envirPairs;
}
::

method::flop
Invert rows and columns in a two dimensional Array (turn inside out).
See also: Function, SequenceableCollection.
code::
[[1, 2, 3], [4, 5, 6]].flop;
[[1, 2, 3], [4, 5, 6], [7, 8]].flop; // shorter array wraps
[].flop; // result is always 2-d.
::

method::multiChannelExpand
Used by UGens to perform multi channel expansion. Same as flop.

method::source
Some UGens return Arrays of OutputProxy when instantiated. This method allows you to
get at the source UGen.
code::
z = Pan2.ar;
z.postln;
z.source.postln;
::

method::fork
Used within Routines and assumes an array of functions, from which subroutines are created. The subroutines are played while the outer Routine carries on. The join parameter expresses after how many subroutines complete the outer Routine is allowed to go on. By default this happens after all subroutines have completed.
code::
// an array of routine functions:
(
a = [
	{ 1.wait; \done_one.postln },
	{ 0.5.wait; \done_two.postln },
	{ 0.2.wait; \done_three.postln }
];
)
// join after 0
(
Routine {
	"join = 0.".postcln;
	a.fork(0); \doneAll.postln;
}.play;
)
// join after 1
(
Routine {
	"join = 1.".postcln;
	a.fork(1); \doneAll.postln;
}.play;
)
// join after all
(
Routine {
	"join = a.size (default).".postcln;
	a.fork; \doneAll.postln;
}.play;
)
::

method::poll
apply an array of Poll units to an array of UGens (see those helpfiles for more details).
code::
(
x = {
	SinOsc.ar([0.1, 0.2], 0).poll * 0.1
}.play;
)
x.trace; // By tracing the Synth you can see the two Poll units we created
x.free
::

method::dpoll
apply an array of Dpoll units to an array of UGens (see those helpfiles for more details).

method::atIdentityHash
This method is used by IdentitySet to search for a key among its members.

method::atIdentityHashInPairs
This method is used by IdentityDictionary to search for a key among its members.

method::asString
Returns a string representing the Array. May not be compilable due to elision (...) of excessive arguments.

method::asCompileString
Returns a string that will compile to return an Array equal to the receiver.

method::isValidUGenInput
Returns true. Arrays are valid UGen inputs.

method::asRawOSC
Returns the OSC message as an Int8Array. Receiver must be a bundle.
code::
[0.1, [\s_new, \default, -1, 1, 1, \freq, 1961]].asRawOSC;
::

subsection:: Bela

method:: belaScope
Send this Array's content to Bela's Oscilloscope (see link::Classes/BelaScope:: for required setup)
argument:: scopeChannel
Bela's oscilloscope channel to start scoping on. This has to be a non-negative number, and can't be changed after scoping starts.

argument:: server
The server on which BelaScope is running. If not specified, it looks for the first server for which BelaScope was already initialized. If none is found, it attempts to initialize a link::Classes/BelaScope:: instance on link::Server#*default::.
returns:: This Array.
